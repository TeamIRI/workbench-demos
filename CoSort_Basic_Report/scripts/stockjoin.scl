### CoSort Copyright 2013 IRI, Inc. ###
### Sort Control Language (SortCL) Program ###


### Job Controls Phase ###
/MONITOR=1
/STATISTICS=..\JobStats\tradinga.stat
/WARNINGSOFF 

#### Input Source Specifications ####
/INFILE=..\InputFiles\nyse-a.txt # tab-delimited format
	/PROCESS=DELIMITED
	/ALIAS=nyse-a
	/FIELD=(ISSUE, TYPE=ASCII, POSITION=1, SEPARATOR="\t")
	/FIELD=(SYMBOL, TYPE=ASCII, POSITION=2, SEPARATOR="\t")
	/FIELD=(LASTTRADE, TYPE=NUMERIC, POSITION=3, PRECISION=2, SEPARATOR="\t")
	/FIELD=(VOLUME, TYPE=NUMERIC, POSITION=4, SEPARATOR="\t")
	/FIELD=(CHANGE, TYPE=NUMERIC, POSITION=5, PRECISION=2, SEPARATOR="\t")
	/FIELD=(PERCENT, TYPE=NUMERIC, POSITION=6, PRECISION=2, SEPARATOR="\t")

/INFILE=..\InputFiles\buys.csv # CSV format
	/PROCESS=CSV
	/ALIAS=buys
	/INSKIP=1 # skip header record
	/FIELD=(SHARES, TYPE=ASCII, POSITION=1, SEPARATOR=",", FRAME='"')
	/FIELD=(SYMBOL, TYPE=ASCII, POSITION=2, SEPARATOR=",", FRAME='"')
	/FIELD=(CLIENT, TYPE=ASCII, POSITION=3, SEPARATOR=",", FRAME='"')
	
#### Action Statement - Sort & Join ####
/JOIN FULL_OUTER NOT_SORTED nyse-a NOT_SORTED buys WHERE NYSE-A.SYMBOL EQ BUYS.SYMBOL 

#### Output File 1 - 2D BI Report with Selection ####
/OUTFILE=stdout # summary record format
	/PROCESS=RECORD
	/HEADREC="                                                    -----------\n"
	/FIELD=(NEW_BALANCE, TYPE=currency, POSITION=50, SIZE=14, PRECISION=2)
	/SUM NEW_BALANCE from (NYSE-A.LASTTRADE * BUYS.SHARES) # expression

/OUTFILE=stdout # detail record format
	/PROCESS=RECORD
	/HEADREC="Client             Symbol  NYSE   Shares  LastTrade  Shares*LT     Ln.\n\n"
	/FIELD=(BUYS.CLIENT, TYPE=ASCII, POSITION=1, SIZE=17)
	/FIELD=(BUYS.SYMBOL, TYPE=ASCII, POSITION=20, SIZE=5)
	/FIELD=(NYSE-A.SYMBOL, TYPE=ASCII, POSITION=28, SIZE=5)
	/FIELD=(BUYS.SHARES, TYPE=ASCII, POSITION=35, SIZE=5)
	/FIELD=(NYSE-A.LASTTRADE, TYPE=NUMERIC, POSITION=45, SIZE=5, PRECISION=2)
	/FIELD=(PRODUCT, TYPE=NUMERIC, POSITION=54, IF NYSE-A.SYMBOL NE BUYS.SYMBOL THEN "" ELSE NYSE-A.LASTTRADE * BUYS.SHARES)
	/FIELD=(SEQUENCER, TYPE=ASCII, POSITION=66, SIZE=4)

##### Output File 2 - Data Interchange Format ####
/OUTFILE=..\Targets\TradingA2.xml
	/PROCESS=XML
	/FIELD=(BUYS.CLIENT, TYPE=ASCII, POSITION=1, SEPARATOR="|", XDEF="/Trades/Buy@Client")
	/FIELD=(BUYS.SYMBOL, TYPE=ASCII, POSITION=2, SEPARATOR="|", XDEF="/Trades/Buy/Symbol")
	/FIELD=(BUYS.SHARES, TYPE=ASCII, POSITION=3, SEPARATOR="|", XDEF="/Trades/Buy/Shares")
	/INCLUDE WHERE NYSE-A.LASTTRADE GT 10 AND BUYS.SHARES GT 0 

#### Output File 3 - HTML Format ####
/OUTFILE=..\Targets\TradingA2.htm # summary record format
	/PROCESS=RECORD
	/DATA="</FONT><B></TD>\n<TD align=right><B><U><FONT SIZE=+2><FONT COLOR='GREEN'>"
	/FIELD=(NEW_BALANCE, TYPE=CURRENCY, SIZE=15)
	/DATA="</FONT></U></B></TD>\n</TR>\n"
	/SUM NEW_BALANCE from (NYSE-A.LASTTRADE * BUYS.SHARES) WHERE SYMBOL NE "RNT"
	/FOOTREC="</TABLE><BR>\nCreated on </B>%s.              <HR></BODY>\n</HTML>", AMERICAN_DATE

/OUTFILE=..\Targets\TradingA2.htm # detail record format
	/PROCESS=RECORD
	/HEADREC="<HTML><HEAD>\n<TITLE>HTML produced by SORTCL               </TITLE>\n</HEAD>\n<BODY> \
 		<H2><FONT COLOR='RED'>Trading Summary                </H2>\n<TABLE CELLPADDING=4 CELLSPACING=1 BORDER COLS=5>\n"
	/OMIT WHERE SYMBOL EQ "RNT" OR SYMBOL EQ "" # selection applied in display
	/DATA="<TR>\n<TD><I>"
	/FIELD=(BUYS.CLIENT, TYPE=ASCII, POSITION=1)
	/DATA="</B></TD>\n<TD align=right><FONT COLOR='BLUE'>"
	/FIELD=(BUYS.SYMBOL, TYPE=ASCII, POSITION=2)
	/DATA="</TD>\n</TR>\n"
